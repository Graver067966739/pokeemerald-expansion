const LOCALID_MOM = 1
const LOCALID_SIBLING = 2

mapscripts YourHouse_MapScripts
{
    MAP_SCRIPT_ON_LOAD
    {
        if (flag (FLAG_SYS_B_DASH))
        {
            setmetatile (5, 7, METATILE_BasicBuilding_Shoes_Taken, FALSE)
            special (DrawWholeMapView)
        }

        if (var (VAR_INTRO_STATE) == 0)
        {
            setobjectxyperm (LOCALID_MOM, 11, 7)
        }
    }
    
    MAP_SCRIPT_ON_FRAME_TABLE
    [
        VAR_INTRO_STATE, 0: WakeUp
    ]
}

script WakeUp
{
    lock
    applymovement (LOCALID_MOM, MomEnter)
    waitmovement (0)
    applymovement (LOCALID_MOM, Common_Movement_ExclamationMark)
    waitmovement (0)
    applymovement (LOCALID_MOM, MomWalkCloser)
    waitmovement (0)
    msgbox ("{PLAYER}\pYou're not awake yet?!\pCome on, get ready\nWe're leaving very soon!$")
    waitmessage
    applymovement (LOCALID_MOM, MomLeaveRoom)
    waitmovement (0)
    setvar (VAR_INTRO_STATE, 1)
    setobjectxy(LOCALID_MOM, 2, 9)
    setobjectnewmovementtype (LOCALID_MOM, MOVEMENT_TYPE_FACE_UP_AND_RIGHT)
    release
    end
}

movement MomEnter
{
    walk_right * 2
    walk_up * 3
    walk_right * 2
    step_end
}

movement MomWalkCloser
{
    walk_right * 2
    walk_up * 2
    walk_in_place_right
    step_end
}

movement MomLeaveRoom
{
    walk_down * 2
    walk_left * 4
    walk_down * 4
    walk_left * 5
    step_end
}

movement FaceDown 
{
    walk_in_place_down
}

script CheckLeave
{
    lock
    iswearingoutfit(OUTFIT_2)
    if (var(VAR_RESULT) == TRUE) 
    {
        applymovement (LOCALID_SIBLING, SiblingLeaveRoom)
        waitmovement (0)
        applymovement (LOCALID_MOM, Common_Movement_ExclamationMark)
        waitmovement (0)
        applymovement (LOCALID_MOM, Common_Movement_FaceDown)
        waitmovement (0)
        msgbox ("Ok good\lyou're all ready\pLet's go!")
        waitmessage
        setvar (VAR_INTRO_STATE, 2)
        getplayerxy (VAR_TEMP_2, VAR_TEMP_3)
        if (var (VAR_TEMP_2) == 3)//If player is on the left
        {
            applymovement (LOCALID_MOM, MomLeaveR)
            waitmovement (0)
            applymovement (LOCALID_SIBLING, SiblingLeaveR)
            waitmovement (0)
        }
        else
        {
            applymovement (LOCALID_MOM, MomLeaveL)
            waitmovement (0)
            applymovement (LOCALID_SIBLING, SiblingLeaveL)
            waitmovement (0)
        }

    }
    else
    {
        applymovement (LOCALID_MOM, Common_Movement_ExclamationMark)
        waitmovement (0)
        applymovement (LOCALID_MOM, Common_Movement_FaceDown)
        waitmovement (0)
        msgbox ("You can't leave\nlike that.\pGo to your dresser\nand change into your\lFANCY ATTIRE.$")
        waitmessage
        applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
        waitmovement (0)
    }

    release
    end
}

movement MomLeaveR 
{
    walk_right * 2
    walk_down * 2
    delay_8
	set_invisible
    step_end
}
movement MomLeaveL 
{
    walk_right
    walk_down * 2
    delay_8
	set_invisible
    step_end
}
movement SiblingLeaveR 
{
    walk_down * 2
    delay_8
	set_invisible
    step_end
}
movement SiblingLeaveL 
{
    walk_left
    walk_down * 2
    delay_8
	set_invisible
    step_end
}

movement SiblingLeaveRoom 
{
    walk_down
    walk_left * 4
    walk_up
    walk_left * 8
    walk_down
    step_end
}

script GetRunningShoes
{
    lockall
    if (!flag (FLAG_SYS_B_DASH))
    {
        msgbox("You put on your running shoes")
        setflag(FLAG_SYS_B_DASH)
        setmetatile (5, 7, METATILE_BasicBuilding_Shoes_Taken, FALSE)
        special (DrawWholeMapView)
    }
    else
    {
        msgbox("You already have\nyour shoes.$")
    }
    waitmessage
    releaseall
    end

}

script Sibling
{
    lockall
    msgbox("Now where was it.{PAUSE 20}.{PAUSE 20}.{PAUSE 20}.")
    //Common_Movement_ExclamationMark
    waitmessage
    releaseall
    end
}

script OpenWardrobe
{
    lockall
    checkoutfitflag(OUTFIT_2) // only returns TRUE and FALSE
    if (var(VAR_RESULT) == FALSE) 
    {
        msgbox("unlocking outfit 2")
        waitmessage
        callnative (Script_UnlockOutfit2)
    }
    FadeScreen(FADE_TO_BLACK)
    callnative(Script_OpenOutfitMenu)
    waitstate
    release
    end
}
